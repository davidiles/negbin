
    model{
    
    #-----------------------------------------
    # Priors
    #-----------------------------------------
    
    #
    prob_detect_0 ~ dunif(0,1)
    
    # Reproduction
    reprod.2.intercept ~ dunif(0.001,50)
    reprod.3.intercept ~ dunif(0.001,50)
    reprod.4.intercept ~ dunif(0.001,50)
    
    log.reprod.2.intercept <- log(reprod.2.intercept)
    log.reprod.3.intercept <- log(reprod.3.intercept)
    log.reprod.4.intercept <- log(reprod.4.intercept)
    
    # Survival
    surv.0.intercept ~ dunif(0,1)
    surv.1.intercept ~ dunif(0,1)
    surv.2.intercept ~ dunif(0,1)
    surv.3.intercept ~ dunif(0,1)
    
    logit.surv.0.intercept <- log(surv.0.intercept/(1-surv.0.intercept))
    logit.surv.1.intercept <- log(surv.1.intercept/(1-surv.1.intercept))
    logit.surv.2.intercept <- log(surv.2.intercept/(1-surv.2.intercept))
    logit.surv.3.intercept <- log(surv.3.intercept/(1-surv.3.intercept))
    
    #effect of cobble
    surv.0.beta.cobble ~ dnorm(0,0.01)
    surv.1.beta.cobble ~ dnorm(0,0.01)
    surv.2.beta.cobble ~ dnorm(0,0.01)
    surv.3.beta.cobble ~ dnorm(0,0.01)
    
    reprod.2.beta.cobble ~ dnorm(0,0.01)
    reprod.3.beta.cobble ~ dnorm(0,0.01)
    reprod.4.beta.cobble ~ dnorm(0,0.01)
    
    #Priors for observation effects
    #Effect of sampling depth on observed count
    beta.fdepth[1] <- 0
    beta.fdepth[2] ~ dnorm(0,0.01) #Link this to mu through log link
    beta.fdepth[3] ~ dnorm(0,0.01) #Link this to mu through log link
    beta.fdepth[4] ~ dnorm(0,0.01) #Link this to mu through log link
    
    # Each age has a different rate parameter (r) and a different zero-term; together these affect variance in observed counts
    for (age in 1:A){
    r[age] ~ dunif(0,10)
    zero.intercept.age[age] ~ dunif(0.001,0.999)
    zero.B0[age] <- log(zero.intercept.age[age]/(1-zero.intercept.age[age]))
    }

    # FIRST YEAR ABUNDANCE - STATE PROCESS
    mu.count[1,1] <- mu.count[1,3]*reprod.2[1] + mu.count[1,4]*reprod.3[1] + mu.count[1,5]*reprod.3[1]
    
    mu.count[1,2] ~ dunif(0.01,500)
    mu.count[1,3] ~ dunif(0.01,500)
    mu.count[1,4] ~ dunif(0.01,500)
    mu.count[1,5] ~ dunif(0.01,500)
    
    #Only a fraction are actually available to be observed (relative detection probability)
    mu.obs[1,1] <- mu.count[1,1]*prob_detect_0
    
    sd.reprod ~ dunif(0,4)
    tau.reprod <- pow(sd.reprod,-2)
    
    sd.surv ~ dunif(0,5)
    tau.surv <- pow(sd.surv,-2)
    
    #-----------------------------------------
    # LIKELIHOOD
    #-----------------------------------------
    
    #Time-varying vital rates
    for (year in 1:Y){
    
    log.reprod.2[year] ~ dnorm(log.reprod.2.intercept + reprod.2.beta.cobble * cobble_year[year], tau.reprod)
    log.reprod.3[year] ~ dnorm(log.reprod.3.intercept + reprod.3.beta.cobble * cobble_year[year], tau.reprod)
    log.reprod.4[year] ~ dnorm(log.reprod.4.intercept + reprod.4.beta.cobble * cobble_year[year], tau.reprod)
    
    reprod.2[year] <- exp(log.reprod.2[year])
    reprod.3[year] <- exp(log.reprod.3[year])
    reprod.4[year] <- exp(log.reprod.4[year])
    
    logit.surv.0[year] ~ dnorm(logit.surv.0.intercept + surv.0.beta.cobble * cobble_year[year], tau.surv)
    logit.surv.1[year] ~ dnorm(logit.surv.1.intercept + surv.1.beta.cobble * cobble_year[year], tau.surv)
    logit.surv.2[year] ~ dnorm(logit.surv.2.intercept + surv.2.beta.cobble * cobble_year[year], tau.surv)
    logit.surv.3[year] ~ dnorm(logit.surv.3.intercept + surv.3.beta.cobble * cobble_year[year], tau.surv)
    
    surv.0[year] <- 1/(1+exp(-logit.surv.0[year]))
    surv.1[year] <- 1/(1+exp(-logit.surv.1[year]))
    surv.2[year] <- 1/(1+exp(-logit.surv.2[year]))
    surv.3[year] <- 1/(1+exp(-logit.surv.3[year]))
    
    }
    
    # FIRST YEAR
    
    # Observations are contingent upon current mean (mu.count)
    for (obs in 1:O){
    
    N[1,1,obs] ~ dnegbin(p[1,1,obs],r[1]) #Age 0 individuals
    N[1,2,obs] ~ dnegbin(p[1,2,obs],r[2]) #Age 1 individuals
    N[1,3,obs] ~ dnegbin(p[1,3,obs],r[3]) #Age 2 individuals
    N[1,4,obs] ~ dnegbin(p[1,4,obs],r[4]) #Age 3 individuals
    N[1,5,obs] ~ dnegbin(p[1,5,obs],r[5]) #Age 4 individuals
    
    # Zero-Inflation for observations
    zero[1,1,obs] ~ dbern(prob[1,1,obs])
    prob[1,1,obs] <- 1/(1+exp(-logit.prob[1,1,obs]))
    logit.prob[1,1,obs] <-  zero.B0[1]
    
    zero[1,2,obs] ~ dbern(prob[1,2,obs])
    prob[1,2,obs] <- 1/(1+exp(-logit.prob[1,2,obs]))
    logit.prob[1,2,obs] <-  zero.B0[2]
    
    zero[1,3,obs] ~ dbern(prob[1,3,obs])
    prob[1,3,obs] <- 1/(1+exp(-logit.prob[1,3,obs]))
    logit.prob[1,3,obs] <-  zero.B0[3]
    
    zero[1,4,obs] ~ dbern(prob[1,4,obs])
    prob[1,4,obs] <- 1/(1+exp(-logit.prob[1,4,obs]))
    logit.prob[1,4,obs] <-  zero.B0[4]
    
    zero[1,5,obs] ~ dbern(prob[1,5,obs])
    prob[1,5,obs] <- 1/(1+exp(-logit.prob[1,5,obs]))
    logit.prob[1,5,obs] <-  zero.B0[5]
    
    #Adjust mu for effort covariates(fdepth)
    mu.count.adjusted.for.effort[1,1,obs] <- exp(log(mu.obs[1,1]) + beta.fdepth[fdepth[1, 1, obs]])
    mu.count.adjusted.for.effort[1,2,obs] <- exp(log(mu.count[1,2]) + beta.fdepth[fdepth[1, 2, obs]])
    mu.count.adjusted.for.effort[1,3,obs] <- exp(log(mu.count[1,3]) + beta.fdepth[fdepth[1, 3, obs]])
    mu.count.adjusted.for.effort[1,4,obs] <- exp(log(mu.count[1,4]) + beta.fdepth[fdepth[1, 4, obs]])
    mu.count.adjusted.for.effort[1,5,obs] <- exp(log(mu.count[1,5]) + beta.fdepth[fdepth[1, 5, obs]])
    
    #Calculate the probability parameter (one of the two key components of the ZINB) based on mu, r, and zero
    p[1,1,obs] <- r[1]/(r[1]+(1-zero[1,1,obs])* mu.count.adjusted.for.effort[1,1,obs]) - 1e-10*zero[1,1,obs]
    p[1,2,obs] <- r[2]/(r[2]+(1-zero[1,2,obs])* mu.count.adjusted.for.effort[1,2,obs]) - 1e-10*zero[1,2,obs]
    p[1,3,obs] <- r[3]/(r[3]+(1-zero[1,3,obs])* mu.count.adjusted.for.effort[1,3,obs]) - 1e-10*zero[1,3,obs]
    p[1,4,obs] <- r[4]/(r[4]+(1-zero[1,4,obs])* mu.count.adjusted.for.effort[1,4,obs]) - 1e-10*zero[1,4,obs]
    p[1,5,obs] <- r[5]/(r[5]+(1-zero[1,5,obs])* mu.count.adjusted.for.effort[1,5,obs]) - 1e-10*zero[1,5,obs]
    
    }
    
    # SUBSEQUENT YEARS
    for (year in 2:Y){
    
    #Population Model
    mu.count[year,1] <- mu.count[year,3]*reprod.2[year] + mu.count[year,4]*reprod.3[year] + mu.count[year,5]*reprod.3[year]
    
    #Only a fraction are actually available to be observed (relative detection probability)
    mu.obs[year,1] <- mu.count[year,1]*prob_detect_0
    
    mu.count[year,2] <- mu.count[year-1,1]*surv.1[year-1]
    mu.count[year,3] <- mu.count[year-1,2]*surv.1[year-1]
    mu.count[year,4] <- mu.count[year-1,3]*surv.2[year-1]
    mu.count[year,5] <- mu.count[year-1,4]*surv.2[year-1]
    
    # OBSERVATION PROCESS (ZERO INFLATED NEGATIVE BINOMIAL)
    #Observations are contingent on current mean for each age class
    for (obs in 1:O){
    
    # Zero-Inflation for observations
    zero[year,1,obs] ~ dbern(prob[year,1,obs])
    prob[year,1,obs] <- 1/(1+exp(-logit.prob[year,1,obs]))
    logit.prob[year,1,obs] <-  zero.B0[1]
    
    zero[year,2,obs] ~ dbern(prob[year,2,obs])
    prob[year,2,obs] <- 1/(1+exp(-logit.prob[year,2,obs]))
    logit.prob[year,2,obs] <-  zero.B0[2]
    
    zero[year,3,obs] ~ dbern(prob[year,3,obs])
    prob[year,3,obs] <- 1/(1+exp(-logit.prob[year,3,obs]))
    logit.prob[year,3,obs] <-  zero.B0[3]
    
    zero[year,4,obs] ~ dbern(prob[year,4,obs])
    prob[year,4,obs] <- 1/(1+exp(-logit.prob[year,4,obs]))
    logit.prob[year,4,obs] <-  zero.B0[4]
    
    zero[year,5,obs] ~ dbern(prob[year,5,obs])
    prob[year,5,obs] <- 1/(1+exp(-logit.prob[year,5,obs]))
    logit.prob[year,5,obs] <-  zero.B0[5]
    
    #Adjust mu for effort covariates(fdepth)
    mu.count.adjusted.for.effort[year,1,obs] <- exp(log(mu.obs[year,1]) + beta.fdepth[fdepth[year, 1, obs]])
    mu.count.adjusted.for.effort[year,2,obs] <- exp(log(mu.count[year,2]) + beta.fdepth[fdepth[year, 2, obs]])
    mu.count.adjusted.for.effort[year,3,obs] <- exp(log(mu.count[year,3]) + beta.fdepth[fdepth[year, 3, obs]])
    mu.count.adjusted.for.effort[year,4,obs] <- exp(log(mu.count[year,4]) + beta.fdepth[fdepth[year, 4, obs]])
    mu.count.adjusted.for.effort[year,5,obs] <- exp(log(mu.count[year,5]) + beta.fdepth[fdepth[year, 5, obs]])
    
    #Probability parameter (one of the two key components of the ZINB) based on mu, r, and zero
    p[year,1,obs] <- r[1]/(r[1]+(1-zero[year,1,obs])* mu.count.adjusted.for.effort[year,1,obs]) - 1e-10*zero[year,1,obs]
    p[year,2,obs] <- r[2]/(r[2]+(1-zero[year,2,obs])* mu.count.adjusted.for.effort[year,2,obs]) - 1e-10*zero[year,2,obs]
    p[year,3,obs] <- r[3]/(r[3]+(1-zero[year,3,obs])* mu.count.adjusted.for.effort[year,3,obs]) - 1e-10*zero[year,3,obs]
    p[year,4,obs] <- r[4]/(r[4]+(1-zero[year,4,obs])* mu.count.adjusted.for.effort[year,4,obs]) - 1e-10*zero[year,4,obs]
    p[year,5,obs] <- r[5]/(r[5]+(1-zero[year,5,obs])* mu.count.adjusted.for.effort[year,5,obs]) - 1e-10*zero[year,5,obs]
    
    N[year,1,obs] ~ dnegbin(p[year,1,obs],r[1])
    N[year,2,obs] ~ dnegbin(p[year,2,obs],r[2])
    N[year,3,obs] ~ dnegbin(p[year,3,obs],r[3])
    N[year,4,obs] ~ dnegbin(p[year,4,obs],r[4])
    N[year,5,obs] ~ dnegbin(p[year,5,obs],r[5])
    
    }
    }
    
for (year in 1:Y){
    mu.breeding[year] <- mu.count[year,3] + mu.count[year,4] + mu.count[year,5]
}

    
    
    }
    
