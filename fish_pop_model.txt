
    model{
    
    #-----------------------------------------
    # Priors
    #-----------------------------------------
    
    # Reproduction
    reprod.2.intercept ~ dunif(0.001,50)
    reprod.3.intercept ~ dunif(0.001,50)
    reprod.4.intercept ~ dunif(0.001,50)
    
    log.reprod.2.intercept <- log(reprod.2.intercept)
    log.reprod.3.intercept <- log(reprod.3.intercept)
    log.reprod.4.intercept <- log(reprod.4.intercept)
    
    # Survival
    surv.1.intercept ~ dunif(0,1)
    surv.2.intercept ~ dunif(0,1)
    surv.3.intercept ~ dunif(0,1)
    
    logit.surv.1.intercept <- log(surv.1.intercept/(1-surv.1.intercept))
    logit.surv.2.intercept <- log(surv.2.intercept/(1-surv.2.intercept))
    logit.surv.3.intercept <- log(surv.3.intercept/(1-surv.3.intercept))
    
    #effect of cobble
    surv.1.beta.cobble ~ dnorm(0,0.01)
    surv.2.beta.cobble ~ dnorm(0,0.01)
    surv.3.beta.cobble ~ dnorm(0,0.01)
    
    reprod.2.beta.cobble ~ dnorm(0,0.01)
    reprod.3.beta.cobble ~ dnorm(0,0.01)
    reprod.4.beta.cobble ~ dnorm(0,0.01)
    
    #Priors for observation effects
    #Effect of sampling depth on observed count
    beta.fdepth[1] <- 0
    beta.fdepth[2] ~ dnorm(0,0.01) #Link this to mu through log link
    beta.fdepth[3] ~ dnorm(0,0.01) #Link this to mu through log link
    beta.fdepth[4] ~ dnorm(0,0.01) #Link this to mu through log link
    
    # Each age has a different rate parameter (r) and a different zero-term; together these affect variance in observed counts
    for (age in 1:4){
      r[age] ~ dunif(0,10)
      zero.intercept.age[age] ~ dunif(0.001,0.999)
      zero.B0[age] <- log(zero.intercept.age[age]/(1-zero.intercept.age[age]))
    }
    
    sd.reprod ~ dunif(0,4)
    tau.reprod <- pow(sd.reprod,-2)
    
    sd.surv ~ dunif(0,5)
    tau.surv <- pow(sd.surv,-2)

    #-----------------------------------------
    # LIKELIHOOD
    #-----------------------------------------
    
    #Time-varying vital rates
    for (year in 1:Y){
      
      log.reprod.2[year] ~ dnorm(log.reprod.2.intercept + reprod.2.beta.cobble * cobble_year[year], tau.reprod)
      log.reprod.3[year] ~ dnorm(log.reprod.3.intercept + reprod.3.beta.cobble * cobble_year[year], tau.reprod)
      log.reprod.4[year] ~ dnorm(log.reprod.4.intercept + reprod.4.beta.cobble * cobble_year[year], tau.reprod)
      
      reprod.2[year] <- exp(log.reprod.2[year])
      reprod.3[year] <- exp(log.reprod.3[year])
      reprod.4[year] <- exp(log.reprod.4[year])
      
      logit.surv.1[year] ~ dnorm(logit.surv.1.intercept + surv.1.beta.cobble * cobble_year[year], tau.surv)
      logit.surv.2[year] ~ dnorm(logit.surv.2.intercept + surv.2.beta.cobble * cobble_year[year], tau.surv)
      logit.surv.3[year] ~ dnorm(logit.surv.3.intercept + surv.3.beta.cobble * cobble_year[year], tau.surv)
      
      surv.1[year] <- 1/(1+exp(-logit.surv.1[year]))
      surv.2[year] <- 1/(1+exp(-logit.surv.2[year]))
      surv.3[year] <- 1/(1+exp(-logit.surv.3[year]))
      
    }
    
    # State Process
    #Population Model

    #FIRST YEAR 
    mu.count[1,1] ~ dunif(0.01,500)
    mu.count[1,2] ~ dunif(0.01,500)
    mu.count[1,3] ~ dunif(0.01,500)
    mu.count[1,4] ~ dunif(0.01,500)
    
    #SECOND YEAR
    mu.count[2,1] ~ dunif(0.01,500)
    mu.count[2,2] <- mu.count[1,1]*surv.1[1]
    mu.count[2,3] <- mu.count[1,2]*surv.2[1]
    mu.count[2,4] <- mu.count[1,3]*surv.2[1]

    #SUBSEQUENT YEARS
    for (year in 3:Y){
      mu.count[year,1] <- mu.count[year-2,2]*reprod.2[year-2] + mu.count[year-2,3]*reprod.3[year-2] + mu.count[year-2,4]*reprod.3[year-2]
      mu.count[year,2] <- mu.count[year-1,1]*surv.1[year-1]
      mu.count[year,3] <- mu.count[year-1,2]*surv.2[year-1]
      mu.count[year,4] <- mu.count[year-1,3]*surv.2[year-1]
    }
    
    #Observation Process
    for (year in 1:Y){
      for (obs in 1:O){

      N[year,1,obs] ~ dnegbin(p[year,1,obs],r[1]) #Age 1 individuals
      N[year,2,obs] ~ dnegbin(p[year,2,obs],r[2]) #Age 2 individuals
      N[year,3,obs] ~ dnegbin(p[year,3,obs],r[3]) #Age 3 individuals
      N[year,4,obs] ~ dnegbin(p[year,4,obs],r[4]) #Age 4 individuals
      
      # Zero-Inflation for observations
      zero[year,1,obs] ~ dbern(prob[year,1,obs])
      prob[year,1,obs] <- 1/(1+exp(-logit.prob[year,1,obs]))
      logit.prob[year,1,obs] <-  zero.B0[1]
      
      zero[year,2,obs] ~ dbern(prob[year,2,obs])
      prob[year,2,obs] <- 1/(1+exp(-logit.prob[year,2,obs]))
      logit.prob[year,2,obs] <-  zero.B0[2]
      
      zero[year,3,obs] ~ dbern(prob[year,3,obs])
      prob[year,3,obs] <- 1/(1+exp(-logit.prob[year,3,obs]))
      logit.prob[year,3,obs] <-  zero.B0[3]
      
      zero[year,4,obs] ~ dbern(prob[year,4,obs])
      prob[year,4,obs] <- 1/(1+exp(-logit.prob[year,4,obs]))
      logit.prob[year,4,obs] <-  zero.B0[4]
      
      #Adjust mu for effort covariates(fdepth)
      mu.count.adjusted.for.effort[year,1,obs] <- exp(log(mu.count[year,1]) + beta.fdepth[fdepth[year, 1, obs]])
      mu.count.adjusted.for.effort[year,2,obs] <- exp(log(mu.count[year,2]) + beta.fdepth[fdepth[year, 2, obs]])
      mu.count.adjusted.for.effort[year,3,obs] <- exp(log(mu.count[year,3]) + beta.fdepth[fdepth[year, 3, obs]])
      mu.count.adjusted.for.effort[year,4,obs] <- exp(log(mu.count[year,4]) + beta.fdepth[fdepth[year, 4, obs]])
      
      #Calculate the probability parameter (one of the two key components of the ZINB) based on mu, r, and zero
      p[year,1,obs] <- r[1]/(r[1]+(1-zero[year,1,obs])* mu.count.adjusted.for.effort[year,1,obs]) - 1e-10*zero[year,1,obs]
      p[year,2,obs] <- r[2]/(r[2]+(1-zero[year,2,obs])* mu.count.adjusted.for.effort[year,2,obs]) - 1e-10*zero[year,2,obs]
      p[year,3,obs] <- r[3]/(r[3]+(1-zero[year,3,obs])* mu.count.adjusted.for.effort[year,3,obs]) - 1e-10*zero[year,3,obs]
      p[year,4,obs] <- r[4]/(r[4]+(1-zero[year,4,obs])* mu.count.adjusted.for.effort[year,4,obs]) - 1e-10*zero[year,4,obs]

      }
    }

    
    for (year in 1:Y){
      mu.breeding[year] <- mu.count[year,2] + mu.count[year,3] + mu.count[year,4]
    }
    
    
    
    }
    
